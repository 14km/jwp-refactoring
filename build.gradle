buildscript {
    ext {
        springBootVersion = '2.4.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE"
    }
}

allprojects {
    group = 'camp.nextstep.edu'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    // fixture 사용
    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        // fixture 사용
        testImplementation(testFixtures(project(':kitchenpos-core')))
    }

    test {
        useJUnitPlatform()
    }
}

project(':kitchenpos-core') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}

project(':kitchenpos-application') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        compile project(':kitchenpos-core')
        compile project(':kitchenpos-infra')
    }
}

project(':kitchenpos-infra') {
    bootJar { enabled = false }
    jar { enabled = true }
    dependencies {
        compile project(':kitchenpos-core')

        runtimeOnly 'com.h2database:h2'
        implementation 'org.flywaydb:flyway-core'
    }
}

project(':kitchenpos-client-api') {
    bootJar { enabled = true }
    jar { enabled = true }

    dependencies {
        compile project(':kitchenpos-core'),
                project(':kitchenpos-application'),
                project(':kitchenpos-infra')

        compile 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
}
